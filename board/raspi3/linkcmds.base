SECTIONS
{
        .text : {
                _rom_begin = .;
                _text_begin = .;
                *(.text*)
                _text_end = .;
        } > REGION_TEXT AT > REGION_TEXT_LOAD

        .rodata : ALIGN(0x10) {
                *(.rodata*)
                _rom_end = .;
        } > REGION_RODATA AT > REGION_RODATA_LOAD

        .bss : ALIGN(0x10) {
                _bss_begin = .;
                *(.bss*)
                *(COMMON)
                _bss_end = .;
        } > REGION_BSS AT > REGION_BSS
        _bss_size = _bss_end - _bss_begin;

        .stack : ALIGN(0x10) {
                _stack_begin = .;
                . += (4 * 4096);
                . = ALIGN(0x10);
                _stack_end = .;
        } > REGION_STACK AT > REGION_STACK

        /DISCARD/ : { *(.eh_frame*) }
        /DISCARD/ : { *(.note*) }
}

exec__bss_begin       = _bss_begin;
exec__bss_end         = _bss_end;
exec__stack_end       = _stack_end;
exec__rom_begin       = _rom_begin;
exec__rom_end         = _rom_end;
exec__addmem_bottom   = _stack_end;
exec__addmem_top      = ORIGIN(REGION_STACK) + LENGTH(REGION_STACK);

